plugins {
    id 'groovy'
    id 'jacoco'
    id 'project-report'
    id 'signing'
    id 'ru.vyarus.java-lib' version '2.4.0'
    id 'ru.vyarus.github-info' version '1.5.0'
    id 'ru.vyarus.quality' version '4.9.0'
    id 'net.researchgate.release' version '3.0.2'
    id 'io.github.gradle-nexus.publish-plugin' version '1.3.0'
    id 'com.github.ben-manes.versions' version '0.46.0'
}

sourceCompatibility = 1.8

wrapper {
    gradleVersion = '7.6.1'
}

repositories { mavenLocal(); mavenCentral() }
dependencies {
    implementation 'com.google.inject:guice:5.1.0'
    implementation 'org.slf4j:slf4j-api:2.0.7'
    implementation 'jakarta.annotation:jakarta.annotation-api:2.1.1'
    optional 'org.javassist:javassist:3.29.2-GA'

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.slf4j:slf4j-nop:1.7.36'
}

group = 'ru.vyarus'
description = 'Guice annotations extensions'

github {
    user = 'xvik'
    license = 'MIT'
}

pom {
    developers {
        developer {
            id 'xvik'
            name 'Vyacheslav Rusakov'
            email 'vyarus@gmail.com'
        }
    }
}

javaLib {
    // java 9 auto module name
    autoModuleName = 'ru.vyarus.guice.annotation.extensions'
    // don't publish gradle metadata artifact
    withoutGradleMetadata()
}

nexusPublishing {
    repositories {
        sonatype {
            username = findProperty('sonatypeUser')
            password = findProperty('sonatypePassword')
        }
    }
}

// skip signing for jitpack (snapshots)
tasks.withType(Sign) {onlyIf { !System.getenv('JITPACK') }}

// Required signing properties for release: signing.keyId, signing.password and signing.secretKeyRingFile
// (https://docs.gradle.org/current/userguide/signing_plugin.html#sec:signatory_credentials)

release.git.requireBranch.set('master')

afterReleaseBuild {
    dependsOn = ['publishToSonatype',
                 'closeAndReleaseSonatypeStagingRepository']
    doLast {
        logger.warn "RELEASED $project.group:$project.name:$project.version"
    }
}

test {
    //useJUnitPlatform()
    testLogging {
        events 'skipped', 'failed'
        exceptionFormat 'full'
    }
    maxHeapSize = '512m'
}

dependencyUpdates.revision = 'release'
