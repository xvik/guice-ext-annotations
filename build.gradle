plugins {
    id 'groovy'
    id 'jacoco'
    id 'project-report'
    id 'ru.vyarus.java-lib' version '2.1.0'
    id 'ru.vyarus.github-info' version '1.2.0'
    id 'ru.vyarus.quality' version '4.6.0'
    id 'com.jfrog.bintray' version '1.8.5'
    id 'net.researchgate.release' version '2.8.1'
    id 'com.github.ben-manes.versions' version '0.38.0'
}

sourceCompatibility = 1.8

wrapper {
    gradleVersion = '6.8'
}

repositories { mavenCentral(); mavenLocal() }
dependencies {
    implementation 'com.google.inject:guice:5.0.1'
    implementation 'org.slf4j:slf4j-api:1.7.30'
    implementation 'javax.annotation:javax.annotation-api:1.3.2'
    optional 'org.javassist:javassist:3.27.0-GA'

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.slf4j:slf4j-nop:1.7.30'
}

group = 'ru.vyarus'
description = 'Guice annotations extensions'

github {
    user = 'xvik'
    license = 'MIT'
}

pom {
    developers {
        developer {
            id 'xvik'
            name 'Vyacheslav Rusakov'
            email 'vyarus@gmail.com'
        }
    }
}

// java 11 auto module name
jar {
    manifest {
        attributes 'Automatic-Module-Name': "$project.group" + '.guice.annotation.extensions'
    }
}

bintray {
    user = findProperty('bintrayUser')
    key = findProperty('bintrayKey')
    publications = ['maven']
    dryRun = false
    publish = true
    pkg {
        repo = 'xvik'
        name = project.name
        desc = project.description
        labels = ['guice', 'slf4j', 'jsr250']
        publicDownloadNumbers = true
        version {
            gpg {
                sign = true
                passphrase = findProperty('gpgPassphrase')
            }
            mavenCentralSync {
                sync = true
                user = findProperty('sonatypeUser')
                password = findProperty('sonatypePassword')
            }
        }
    }
}

afterReleaseBuild {
    dependsOn = [bintrayUpload]
    doLast {
        logger.warn "RELEASED $project.group:$project.name:$project.version"
    }
}

test {
    testLogging {
        events 'skipped', 'failed'
        exceptionFormat 'full'
    }
    maxHeapSize = '512m'
}

dependencyUpdates.revision = 'release'
jacocoTestReport.reports.xml.enabled = true

// disable gradle metadata publishing (because it confuse A LOT)
tasks.withType(GenerateModuleMetadata) {
    enabled = false
}

task showDependenciesTree(dependsOn: 'htmlDependencyReport', group: 'help', description:
        'Generates dependencies tree report and opens it in browser') {
    doLast {
        java.awt.Desktop.getDesktop().open(file('build/reports/project/dependencies/root.html'))
    }
}
